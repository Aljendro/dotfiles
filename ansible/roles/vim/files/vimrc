" Settings vimrc file.
"
" Maintainer: Alejandro Alvarado <alejandro.alvarado0650144@gmail.com>
"

""""""""""""""""""""
"""""""""""""""""""" Defaults Customizations
""""""""""""""""""""

" Set the font
if has("gui_running")
  set guifont=Ubuntu\ Mono\ Bold\ 14
endif

" Add line numbers
set number
set relativenumber
" Toggle in insert mode
augroup numbertoggle
  autocmd!
  autocmd BufEnter,FocusGained,InsertLeave * set relativenumber
  autocmd BufLeave,FocusLost,InsertEnter   * set norelativenumber
augroup END

" Default to spaces
set expandtab
set tabstop=2
set shiftwidth=2
set autoindent

" show the cursor position all the time
set ruler
" display incomplete commands
set showcmd
" display completion matches in a status line
set wildmenu
" Set the amount of time vim waits to write to the swap file
set updatetime=100
" TextEdit might fail if hidden is not set
set hidden
" Give more space for displaying messages.
set cmdheight=2
" Don't pass messages to |ins-completion-menu|.
set shortmess+=c
" Always show the signcolumn, otherwise it would shift the text each time
" diagnostics appear/become resolved.
set signcolumn=yes

" time out for mappings
set timeout
set timeoutlen=1000

" time out for key codes
set ttimeout
set ttimeoutlen=100

" Show @@@ in the last line if it is truncated.
set display=truncate

" Show a few lines of context around the cursor.  Note that this makes the
" text scroll if you mouse-click near the start or end of the window.
set scrolloff=5

" Do incremental searching when it's possible to timeout.
if has('reltime')
  set incsearch
endif

" Do not recognize octal numbers for Ctrl-A and Ctrl-X, most users find it
" confusing.
set nrformats-=octal

" Don't use Ex mode, use Q for formatting.
map Q gq

" CTRL-U in insert mode deletes a lot.  Use CTRL-G u to first break undo,
" so that you can undo CTRL-U after inserting a line break.
inoremap <C-U> <C-G>u<C-U>

" Switch syntax highlighting on when the terminal has colors or when using the
" GUI (which always has colors).
if &t_Co > 2 || has("gui_running")
  " Revert with ":syntax off".
  syntax on
endif

" Enable file type detection.
" Use the default filetype settings, so that mail gets 'tw' set to 72,
" 'cindent' is on in C files, etc.
" Also load indent files, to automatically do language-dependent indenting.
" Revert with ":filetype off".
filetype plugin indent on

" Put these in an autocmd group, so that you can revert them with:
" ":augroup vimStartup | au! | augroup END"
augroup vimStartup
  autocmd!

  " When editing a file, always jump to the last known cursor position.
  " Don't do it when the position is invalid, when inside an event handler
  " (happens when dropping a file on gvim) and for a commit message (it's
  " likely a different one than last time).
  autocmd BufReadPost *
    \ if line("'\"") >= 1 && line("'\"") <= line("$") && &ft !~# 'commit'
    \ |   exe "normal! g`\""
    \ | endif

  " Removing the o option removes adding a comment when open new line
  autocmd Filetype * set formatoptions-=o
augroup END

" Whenever Vim writes things to a buffer
augroup vimWrite
  autocmd!
  autocmd FileType c,cpp,java,javascript,vim,python,yaml autocmd BufWritePre <buffer> %s/\s\+$//e
augroup END

if has('langmap') && exists('+langremap')
  " Prevent that the langmap option applies to characters that result from a
  " mapping.  If set (default), this may break plugins (but it's backward
  " compatible).
  set nolangremap
endif

""""""""""""""""""""
"""""""""""""""""""" Abbreviations
""""""""""""""""""""

iabbrev @@ Alejandro Alvarado <alejandro.alvarado0650144@gmail.com>
iabbrev """ """"""""""""""""""""

""""""""""""""""""""
"""""""""""""""""""" Misc. Mappings
""""""""""""""""""""

nnoremap <space> <Nop>
let mapleader = " "

" Quick edit vimrc (plus cursor disappearing workaround (!ls<cr><cr>))
nnoremap <leader>ev :vsplit $DOTFILES_DIR/ansible/roles/vim/files/vimrc<cr>:!ls<cr><cr>G
nnoremap <leader>sv :w<cr>:source ~/.vimrc<cr>
nnoremap <leader>sve :w<cr>:source ~/.vimrc<cr>:q<cr>:!ls<cr><cr>

" Move around tabs easily

" Create a new tab at the end
nnoremap <leader>tn :<C-U>tabnew<cr>:<C-U>tabmove<cr>
" Close the tab
nnoremap <leader>tc :<C-U>tabclose<cr>

" Move between windows easily
nnoremap <C-J> <C-W><C-J>
nnoremap <C-K> <C-W><C-K>
nnoremap <C-L> <C-W><C-L>
nnoremap <C-H> <C-W><C-H>

""""""""""""""""""""
"""""""""""""""""""" Plugins
""""""""""""""""""""

call plug#begin('~/.vim/plugged')

Plug 'tomasr/molokai'
Plug 'itchyny/lightline.vim'
Plug 'airblade/vim-gitgutter'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'scrooloose/nerdtree'
Plug 'scrooloose/nerdcommenter'
Plug 'tpope/vim-fugitive'
Plug 'sirver/ultisnips'
Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
Plug 'junegunn/fzf.vim'
Plug 'neoclide/coc.nvim', {'branch': 'release'}
Plug 'terryma/vim-multiple-cursors'

call plug#end()

""""""""""""""""""""
"""""""""""""""""""" Plugin Customizations
""""""""""""""""""""

"""""""""""""""""""" Molokai Theme

colorscheme molokai

"""""""""""""""""""" Lightline

set laststatus=2

"""""""""""""""""""" NERDTree (File Tree Viewer Plugin)

nnoremap <leader>n :<C-U>NERDTreeFind<cr>

let NERDTreeShowLineNumbers = 1
let NERDTreeQuitOnOpen = 3

augroup customNERDTree
  autocmd!
  " Close NERDTree if its the last open window
  autocmd BufEnter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
  autocmd FileType nerdtree nnoremap <buffer> <leader>n :<C-U>NERDTreeToggle<cr>
augroup END

"""""""""""""""""""" NERDComenter

" Add spaces after comment delimiters by default
let g:NERDSpaceDelims = 1

" Allow commenting and inverting empty lines (useful when commenting a region)
let g:NERDCommentEmptyLines = 1

" Enable NERDCommenterToggle to check all selected lines is commented or not
let g:NERDToggleCheckAllLines = 0

"""""""""""""""""""" Fugitive (Git Plugin)

" Open diffs of all the different changed files in tabs
nnoremap <leader>dt :<C-U>Git difftool -y<cr>
" Choose left buffer
nnoremap <leader>dl :<C-U>diffget //2<cr>
" Choose the right buffer
nnoremap <leader>dr :<C-U>diffget //3<cr>
" Refresh difftool
nnoremap <leader>du :<C-U>diffupdate
" Open merge conflicts in different tabs
nnoremap <leader>gmc :<C-U>Git mergetool<cr>
" Open git blame with commit and author
nmap <leader>gb :<C-U>Git blame<cr>A

cabbrev gco Git checkout
cabbrev gcb Git checkout -b
cabbrev gac Git commit -a -m
cabbrev gsta Git stash push -u
cabbrev gstd Git stash drop
cabbrev gstl Git stash list
cabbrev gstp Git stash pop

"""""""""""""""""""" Snippets

" Trigger configuration.
let g:UltiSnipsExpandTrigger="<C-g>"
let g:UltiSnipsListSnippets="<S-C-g>"
let g:UltiSnipsJumpForwardTrigger="<C-j>"
let g:UltiSnipsJumpBackwardTrigger="<C-k>"
let g:UltiSnipsEditSplit="vertical"

" Directories to search for snippets
let g:snippetPath=$DOTFILES_DIR.'/ansible/roles/vim/files/snippets/'
let g:UltiSnipsSnippetDirectories=[snippetPath]

" Quick Edit the snippets
nnoremap <expr> <leader>es ':<C-U>tabedit ' . g:snippetPath


"""""""""""""""""""" Coc (Completion Plugin)

" Use tab for trigger completion with characters ahead and navigate.
" NOTE: Use command ':verbose imap <tab>' to make sure tab is not mapped by
" other plugin before putting this into your config.
inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

" Use to trigger completion.
inoremap <silent><expr> <c-space> coc#refresh()

" Use <cr> to confirm completion, `<C-g>u` means break undo chain at current
" position. Coc only does snippet and additional edit on confirm.
" <cr> could be remapped by other vim plugin, try `:verbose imap <CR>`.
if exists('*complete_info')
  inoremap <expr> <cr> complete_info()["selected"] != "-1" ? "\<C-y>" : "\<C-g>u\<CR>"
else
  inoremap <expr> <cr> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"
endif

" Use `[g` and `]g` to navigate diagnostics
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" GoTo code navigation.
nmap <silent> <leader><leader>d <Plug>(coc-definition)
nmap <silent> <leader><leader>td <Plug>(coc-type-definition)
nmap <silent> <leader><leader>i  <Plug>(coc-implementation)
nmap <silent> <leader><leader>r <Plug>(coc-references)

" Use show documentation in preview window.
nnoremap <silent> <leader><leader>D :call <SID>show_documentation()<CR>

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  else
    call CocAction('doHover')
  endif
endfunction

" Mappings using CoCList:
" Show all diagnostics.
nnoremap <silent> <leader>ad  :<C-u>CocList diagnostics<cr>
" Manage extensions.
nnoremap <silent> <leader>ae  :<C-u>CocList extensions<cr>
" Show commands.
nnoremap <silent> <leader>ac  :<C-u>CocList commands<cr>
" Find symbol of current document.
nnoremap <silent> <leader>ao  :<C-u>CocList outline<cr>
" Search workspace symbols.
nnoremap <silent> <leader>as  :<C-u>CocList -I symbols<cr>
" Resume latest coc list.
nnoremap <silent> <leader>alr  :<C-u>CocListResume<CR>

